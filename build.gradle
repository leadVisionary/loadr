group 'com.visionarysoftwaresolutions'
version '0.1-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Test Structure
sourceSets {
    integrationTest {
        groovy.srcDir file('src/test/integration/groovy')
        resources.srcDir file('src/test/integration/resources')
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }

    test {
        groovy {
            srcDirs = ['src/test/unit/groovy']
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    testLogging {
        events "passed", "skipped", "failed"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.codehaus.groovy', name:'groovy-all', version:'2.4.6'
    compile group: "org.codehaus.gpars", name: "gpars", version:"1.2.1"
    compile group: 'com.amazonaws', name: 'aws-java-sdk-dynamodb', version:'1.10.65'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4'
    testCompile group: 'cglib', name: 'cglib', version: '3.2.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}